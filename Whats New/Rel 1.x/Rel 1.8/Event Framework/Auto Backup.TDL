;; Sri Ganeshji : Sri Balaji : Sri Pitreshwarji : Sri Durgaji : Sri Venkateshwara

/*

This code demonstrates how auto backup or periodic backup can be set.
Action Backup Company is used to achieve the same.

This TDL helps to minimize data loss in case of corruption provide the Auto Backup Feature is 
enabled.

*/

[Function: Backup Function]

	Variable	: Backup Det Var: String

	00	: IF 	: ##EnableAutoBackup
	10	: 	IF		: ##AutoBackupPrompt
	20	: 		QUERYBOX 	: "Backup Company ?" : Yes:No
	30	: 		IF	: $$LastResult
	40	: 			CALL	: Backup Company
	50	: 		ENDIF
	60	: 	ELSE	:
	70	: 		CALL	: Backup Company
	80	: 	ENDIF
	90	: ENDIF

[Function: Backup Company]
	
	10	: SET	: BackupDetVar	: @@DestPath + ", " + ##SVCurrentPath + ", " + ##SVCurrentCompany + +
								  ", " + @@CoNumber
	20	: BACKUP COMPANY		: ","			: ##BackupDetVar
	30	: SET					: SVBackupPath	: @@DestPath

[System: Formula]

	DestPath	: ##DestinationPath + "\" + @@DateForm + "\" + @@TimeForm
	CoNumber	: $$String:($CompanyNumber:Company:##SVCurrentCompany):5
	DateForm	: $$String:$$MachineDate
	TimeForm	: @@HrsForm + @@MtsForm

	HrsForm		: If $$StringPart:$$MachineTime:0:2 CONTAINS ":" +
				  Then $$StringPart:$$MachineTime:0:1 +
				  Else $$StringPart:$$MachineTime:0:2

	MtsForm		: if $$StringPart:$$MachineTime:0:2 CONTAINS ":" +
				  Then $$StringPart:$$MachineTime:2:2 +
				  Else $$StringPart:$$MachineTime:3:2

;; Enable configuration options - used in General of F12 Configuration

[#Part: System Common]

	Add	: Lines	: Enable Auto Backup, Auto Backup Path, Auto Backup Path Info, Auto Backup Prompt

	[Line: Enable Auto Backup]

		Field: Long Prompt, Enable Auto Backup
		Local: Field: Long Prompt : Info 	: $$LocaleString:"Enable Automatic Backup Feature?"

		[Field: Enable Auto Backup]

			Use         : Logical Field
			Modifies    : EnableAutoBackup
	
	[Line: Auto Backup Path]

		Field: Long Prompt, Auto Backup Path
		Local: Field: Long Prompt : Info 	: $$LocaleString:"Destination Path?"
		Local: Field: Long Prompt : Inactive: NOT #EnableAutoBackup
		Local: Field: Long Prompt : Indent	: 2

		[Field: Auto Backup Path]

			Use         : Name Field
			Modifies    : DestinationPath
			Inactive	: NOT #EnableAutoBackup
			Set As		: If $$IsEmpty:##DestinationPath Then "C:\TallyDataBackup" else ##DestinationPath

	[Line: Auto Backup Path Info]

		Field: Simple Field
		Local: Field: Simple Field : Info 	 : $$LocaleString:"(Subfolders for System Date and Time will be created within)"
		Local: Field: Simple Field : Inactive: NOT #EnableAutoBackup
		Local: Field: Simple Field : Style   : Small Italic
		Indent: 1

	[Line: Auto Backup Prompt]

		Field: Long Prompt, Auto Backup Prompt
		Local: Field: Long Prompt : Info 	: $$LocaleString:"Confirmation from user?"
		Local: Field: Long Prompt : Inactive: NOT #EnableAutoBackup
		Local: Field: Long Prompt : Indent	: 2

		[Field: Auto Backup Prompt]

			Use         : Logical Field
			Modifies    : AutoBackupPrompt
			Inactive	: NOT #EnableAutoBackup
			Set As		: If $$IsEmpty:##AutoBackupPrompt Then	Yes else ##AutoBackupPrompt

;; Variable Definition

[Variable: EnableAutoBackup]

	Type		: Logical
	Persistent	: Yes

[Variable: DestinationPath]

	Type		: String
	Persistent	: Yes

[Variable: AutoBackupPrompt]

	Type		: Logical
	Persistent	: Yes

[System: Variable]

	EnableAutoBackup: Yes
	DestinationPath	: "C:\TallyDataBackup"
	AutoBackupPrompt: Yes

;; End-of-File
